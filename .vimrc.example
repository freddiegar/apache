" VIM Config
" @see https://blog.joren.ga/tools/vim-learning-steps
" Build-in improve % option (works with if statements)
runtime macros/matchit.vim

" @see https://vim.fandom.com/wiki/Example_vimrc
" @see https://vim.fandom.com/wiki/Best_Vim_Tips
" @see https://www.shortcutfoo.com/blog/top-50-vim-configuration-options/
" @See :h quickref
set nomodeline
set exrc
set secure
set hidden
set wildmenu
set wildignore+=*/tmp/*,*.so,*.swp,*.zip,.git,.vscode,.idea
set lazyredraw
set nobackup
set nowritebackup
set noswapfile
set path=.,,

" Search
set hlsearch
set incsearch
set smartcase
set ignorecase
set gdefault

" Better Completion
set complete-=i
set complete=.,w,b,u,t
set completeopt=longest,menuone,preview

" Custom Interface
set title
set novisualbell
set autoread
set backspace=indent,eol,start
set clipboard=unnamedplus
set splitbelow
set splitright
set signcolumn=yes

if has('mouse')
    set mouse=n
endif

" Custom Render
syntax enable
set nowrap
set display+=lastline
set encoding=utf-8
set linebreak
set scrolloff=1
set sidescrolloff=5

" Custom View
set number
set numberwidth=1
set relativenumber
set cursorline
set showmatch
set list
set listchars=space:·,tab:»-
set colorcolumn=120
set textwidth=120
set synmaxcol=800
set winheight=1
set winminheight=0
set winheight=999
set updatetime=300

" Custom identation
" set autoindent
set softtabstop=4
set shiftwidth=4
set expandtab
set fileformat=unix

" Enable folding : Hit za
set nofoldenable
set foldmethod=indent
set foldnestmax=10
set foldlevel=99

" Statusline
let g:currentmode={
   \ 'n'  : 'NORMAL   ',
   \ 'v'  : 'VISUAL   ',
   \ 'V'  : 'V-LINE   ',
   \ '' : 'V-BLOCK  ',
   \ 'i'  : 'INSERT   ',
   \ 'R'  : 'REPLACE  ',
   \ 'Rv' : 'V-REPLACE',
   \ 'c'  : 'COMMAND  ',
   \ '!'  : 'SHELL    ',
   \ 't'  : 'TERMINAL ',
   \}

function! ChangeStatuslineColor() abort
    if (mode() =~# '\v(n|no)')
        execute "highlight! StatusLine guifg='#1d2021' guibg='#7c6f64' ctermfg=234 ctermbg=243"
    elseif (mode() =~# '\v(v|V|t)' || g:currentmode[mode()] ==# 'V-BLOCK  ')
        execute "highlight! StatusLine guifg='#fc802d' guibg='#1a2528' ctermfg=172 ctermbg=237"
    elseif (mode() ==# 'i')
        execute "highlight! StatusLine guifg='#84a598' guibg='#1a2528' ctermfg=109 ctermbg=237"
    elseif (mode() ==# 'R')
        execute "highlight! StatusLine guifg='#8fbf7f' guibg='#1a2528' ctermfg=72 ctermbg=237"
    endif

    return ''
endfunction

function! GitBranch() abort
    return system("git rev-parse --abbrev-ref HEAD 2>/dev/null | tr -d '\n'")
endfunction

function! StatuslineGit() abort
    let l:branchname = GitBranch()
    return strlen(l:branchname) > 0 ? l:branchname : ''
endfunction

set showcmd
set noruler
set noshowmode
set shortmess+=WFAIca
set shortmess-=S
set laststatus=2

set statusline=
" set statusline+=%#LineNr#
set statusline+=%{ChangeStatuslineColor()}
set statusline+=\ %n
set statusline+=\ %{g:currentmode[mode()]}
set statusline+=\ %f
set statusline+=%=
set statusline+=\%m
set statusline+=\%r
" set statusline+=\ ascii:%b
set statusline+=\ l:%3l/%3L\ c:%3c
set statusline+=\ %{&fileencoding?&fileencoding:&encoding}
set statusline+=\ %{StatuslineGit()}
" set statusline+=\ @\ %{strftime(\"%H:%M\")}

" Maps
let &t_TI = ""
let &t_TE = ""
let mapleader = "\<Space>"
let maplocalleader = "\<Space>"
map <silent> <Leader><Esc> :call popup_clear(1)<Enter>

" Purify
noremap <Up> <Nop>
noremap <Down> <Nop>
noremap <Left> <Nop>
noremap <Right> <Nop>
" inoremap <Esc> <Nop> " In multicursor it fails :(

" Utility
nnoremap j gj
nnoremap k gk
nnoremap Q @@
nnoremap Y y$

" Shortcuts
nnoremap <silent> <Enter> :nohlsearch<Enter>
nnoremap <silent> T _vg_
nnoremap <silent> <Leader>y "+y
nnoremap <silent> <Leader>d "_d
nnoremap <silent> <Leader>w :update<Enter>
nnoremap <silent> <Leader>e :update<Enter>
nnoremap <silent> <Leader>q :update<Enter>:bd<Enter>
nnoremap <silent> <Leader>f :echo expand("%:p")<Enter>
nnoremap <silent> <Leader>c :execute "normal! mcA,\e`c"<Enter>
nnoremap <silent> <Leader>s :execute "normal! mcA;\e`c"<Enter>
nnoremap <silent> <Leader>ga :AsyncRun git add %:p<Enter> :echo "Added: " . expand("%")<Enter>
inoremap <silent> jk <Esc>
inoremap <silent> jj <Esc>

" Tabs navigation
nnoremap <silent> <Leader>b :ls<Enter>:buffer<Space>
nnoremap <silent> <Leader>j :if &modifiable && !&readonly && &modified <Enter> :update<Enter> :endif<Enter>:bprevious<Enter>
nnoremap <silent> <Leader>k :if &modifiable && !&readonly && &modified <Enter> :update<Enter> :endif<Enter>:bnext<Enter>

" Better split switching
map <C-h> <C-W>h
map <C-j> <C-W>j
map <C-k> <C-W>k
map <C-l> <C-W>l

if has('terminal')
    nnoremap <silent> <C-S-X> :term<Enter>
    tnoremap <Esc><Esc> <C-\><C-N>:bd!<Enter>

    " Mappings to move out from terminal to other views
    tnoremap <C-h> <C-w>h
    tnoremap <C-j> <C-w>j
    tnoremap <C-k> <C-w>k
    tnoremap <C-l> <C-w>l
endif

if !has('gui_running')
    set notimeout
    set ttimeout
    set ttimeoutlen=10

    augroup FastEscape
        autocmd!

        autocmd InsertEnter * set timeoutlen=0
        autocmd InsertLeave * set timeoutlen=1000
    augroup END
endif

" Plugins
call plug#begin('~/.vim/plugged')
Plug 'morhetz/gruvbox'
Plug 'tpope/vim-commentary'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'wellle/targets.vim'
Plug 'michaeljsmith/vim-indent-object'
Plug 'justinmk/vim-sneak'
Plug 'machakann/vim-swap'
Plug 'Raimondi/delimitMate'
Plug 'luochen1990/rainbow'
Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
Plug 'junegunn/fzf.vim'
Plug 'vim-syntastic/syntastic'
Plug 'StanAngeloff/php.vim'
Plug 'terryma/vim-multiple-cursors'
Plug 'vim-test/vim-test'
Plug 'vim-scripts/autotags'
Plug 'arnaud-lb/vim-php-namespace'
Plug 'SirVer/ultisnips'
Plug 'sniphpets/sniphpets'
Plug 'vim-vdebug/vdebug'
Plug 'preservim/tagbar'
" Enable namespace set auto in snippets
Plug 'phpactor/phpactor', {'for': 'php', 'branch': 'master', 'do': 'composer install --no-dev -o'}
" Enable autocompletion
Plug 'neoclide/coc.nvim', {'branch': 'release'} " After :CocInstall coc-phpls
Plug 'skywind3000/asyncrun.vim'
Plug 'airblade/vim-gitgutter'
call plug#end()

" Use Syntastic to diagnostics
" Not use PHPActor as LSP
" :CocConfig add append
"{
"    "diagnostic.enable": false,
"    "diagnostic.enableSign": false,
"}


" Theme
if has('termguicolors')
    set termguicolors
endif

let g:gruvbox_contrast_dark = 'hard'
let g:gruvbox_italic = 1

colorscheme gruvbox

" DelitMate
let g:delimitMate_expand_cr = 1
let g:delimitMate_smart_quotes = 1
let g:delimitMate_expand_inside_quotes = 0
let g:delimitMate_smart_matchpairs = '^\%(\w\|\$\)'
imap <expr> <Enter> pumvisible() ? "\<C-y>" : "<Plug>delimitMateCR"

" Multiple Cursors
let g:multi_cursor_select_all_word_key = '<C-a>'
let g:multi_cursor_select_all_key      = 'g<C-a>'

" Snippets
let g:UltiSnipsEditSplit = 'vertical'
let g:UltiSnipsExpandTrigger='<Tab>'
let g:UltiSnipsJumpForwardTrigger = '<Tab>'
let g:UltiSnipsJumpBackwardTrigger='<S-Tab>'
let g:UltiSnipsUsePythonVersion = 3

" PHPVim
let g:php_version_id = 70400

" Vim Snake
let g:sneak#label = 1

" TagBar
" @see https://github.com/preservim/tagbar
nnoremap <silent> <F8> :TagbarToggle<Enter>

" Fzf
" @see https://github.com/junegunn/fzf.vim
" @see https://jdhao.github.io/2018/11/05/fzf_install_use/#installation
nnoremap <silent> <Leader>o :GFiles<Enter>
nnoremap <silent> <Leader>p :Files<Enter>

" Vim Tests
" https://github.com/vim-test/vim-test
let g:test#strategy = 'vimterminal'
let g:test#php#phpunit#options = '--testdox --no-coverage --stop-on-failure'
nnoremap <silent> <Leader>tt :TestNearest<Enter>
nnoremap <silent> <Leader>tf :TestFile<Enter>
nnoremap <silent> <Leader>ts :TestSuite<Enter>
nnoremap <silent> <Leader>tl :TestLast<Enter>
nnoremap <silent> <Leader>tg :TestVisit<Enter>

" Syntastic
" @see https://github.com/vim-syntastic/syntastic
set statusline+=%#warningmsg#
set statusline+=\ %{SyntasticStatuslineFlag()}
set statusline+=%*

let g:syntastic_stl_format = '%fe!'
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 0
let g:syntastic_enable_balloons = 0
let g:syntastic_php_checkers = ['php']
let g:syntastic_auto_loc_list = 0
let g:syntastic_enable_highlighting = 0
let g:syntastic_error_symbol = 'E'
let g:syntastic_warning_symbol = 'W'
let g:syntastic_style_error_symbol = 'S'
let g:syntastic_style_warning_symbol = 'S'

" Vim PHP Namespaces
let g:php_namespace_sort_after_insert = 1

function! IPhpInsertUse() abort
    call PhpInsertUse()
    call feedkeys('a',  'n')
endfunction

function! PhpCSFixer() abort
    if bufname("%") == ''
        echohl WarningMsg
        echo 'Save file first!. Canceled.'
        echohl None
        return 0
    endif

    let configfile = !filereadable(expand('.php_cs')) ? '/var/www/html/freddiegar/services/.php_cs' : '.php_cs'

    silent write
    let result = system('php-cs-fixer fix ' . bufname("%") . ' --config="' . configfile .'"')
    silent :edit!
endfunction

" Vim Debug
let g:vdebug_keymap = {
\    'run' : '<F5>',
\    'step_into' : '<F7>',
\    'step_over' : '<F8>',
\    'step_out' : '<S-F8>',
\    'close' : '<S-F5>',
\    'detach' : '<F10>',
\    'set_breakpoint' : '<C-F8>',
\    'eval_visual' : '<Leader>xe'
\}

if !exists('g:vdebug_options')
    let g:vdebug_options = {}
endif

let g:vdebug_options = {
\    'port' : 9000,
\    'timeout' : 10,
\    'on_close' : 'detach',
\    'break_on_open' : 0,
\    'watch_window_style' : 'compact',
\    'simplified_status' : 1,
\    'continuous_mode' : 1,
\    'ide_key' : 'PHPSTORM'
\}

" COC Completion
" @see https://github.com/neoclide/coc.nvim
" Use <c-space> to trigger completion.
inoremap <silent> <expr> <c-@> coc#refresh()
" Make <Enter> and <Leader><Tab> auto-select the first completion item
inoremap <silent> <expr> <Enter> pumvisible() ? coc#_select_confirm()
                              \: "\<C-g>u\<Enter>\<C-r>=coc#on_enter()\<Enter>"
inoremap <silent> <expr> <Leader><Tab> pumvisible() ? coc#_select_confirm()
                              \: "\<C-g>u\<Enter>\<C-r>=coc#on_enter()\<Enter>"

" GoTo code navigation.
nmap <silent> gd <Plug>(coc-definition)
nmap <silent> gi <Plug>(coc-implementation)
nmap <silent> gr <Plug>(coc-references)

" Use K to show documentation in preview window.
nnoremap <silent> K :call <SID>show_documentation()<CR>

function! s:show_documentation() abort
    if (index(['vim','help'], &filetype) >= 0)
        execute 'h ' . expand('<cword>')
    elseif (coc#rpc#ready())
        call CocActionAsync('doHover')
    else
        execute '!' . &keywordprg . " " . expand('<cword>')
    endif
endfunction

" Remap <C-f> and <C-b> for scroll float windows/popups. (Used in long file definitions)
if has('patch-8.2.0750')
  nnoremap <silent><nowait><expr> <C-f> coc#float#has_scroll() ? coc#float#scroll(1) : "\<C-f>"
  nnoremap <silent><nowait><expr> <C-b> coc#float#has_scroll() ? coc#float#scroll(0) : "\<C-b>"
endif

" @see https://github.com/vim/vim/issues/4738
nnoremap gx :call OpenURLUnderCursor()<CR>

function! OpenURLUnderCursor() abort
    let s:uri = expand('<cWORD>')
    let s:uri = substitute(s:uri, '?', '\\?', '')
    let s:uri = shellescape(s:uri, 1)

    if s:uri != ''
        silent execute "!/opt/firefox/firefox '" . s:uri . "'"
        :redraw!
    endif
endfunction

" https://vim.fandom.com/wiki/Faster_loading_of_large_files
" file is large from 2mb
let g:LargeFile = 1024 * 1024 * 2

augroup LargeFile
    autocmd!

    autocmd BufReadPre * let f=getfsize(expand("<afile>")) | if f > g:LargeFile || f == -2 | call LargeFile() | endif
augroup END

function! LargeFile() abort
    " No syntax highlighting
    set eventignore+=FileType
    setlocal bufhidden=unload
    setlocal buftype=nowrite
    setlocal undolevels=-1

    autocmd VimEnter *  echo "The file is larger than " . (g:LargeFile / 1024 / 1024) . " MB, so some options are changed."
endfunction

" CTags
" @see https://github.com/vim-scripts/autotags
let g:autotags_no_global = 0
let g:autotags_cscope_file_extensions = '.php .h .c'
let g:autotags_ctags_global_include = ''
let g:autotags_ctags_opts = '--exclude=.git --exclude=bin --exclude=var --exclude=storage --exclude=database --c++-kinds=+p --fields=+iaS --extra=+q'

" GitGutter
" @see https://github.com/airblade/vim-gitgutter
let g:gitgutter_sign_priority = 10000
let g:gitgutter_sign_allow_clobber = 0
let g:gitgutter_set_sign_backgrounds = 1
let g:gitgutter_preview_win_floating = 1
let g:gitgutter_close_preview_on_escape = 1

" Rainbow
" @see https://github.com/luochen1990/rainbow
let g:rainbow_active = 1

nnoremap <Leader>l :call CreateArgumentsList()<Enter>
function! CreateArgumentsList() abort
    let saved_unnamed_register = @@

    if match(getline('.'), '(') > 0 && match(getline('.'), ')') > 0 && match(getline('.'), ',') > 0
        execute "normal! _f(vi(y"

        let command_string = ''
        let arguments_list = split(@@, ',')

        for argument in arguments_list
            let command_string .= "\t" . trim(argument) . (len(arguments_list) > 1 ? ',' : '') . "\r"
            call remove(arguments_list, 0)
        endfor

        execute "normal! \"_di(i\r" . command_string . "\eJkg_"

        echom 'Arguments list created'
    else
        echohl WarningMsg
        echom 'Arguments list not found'
        echohl None
    endif

    let @@ = saved_unnamed_register
endfunction

augroup AutoCommands
    autocmd!

    " Reload after save
    autocmd BufWritePost ~/.vimrc source ~/.vimrc

    " Return to last edit position when opening files
    autocmd BufReadPost *
         \ if line("'\"") > 0 && line("'\"") <= line("$") |
         \   execute "normal! g`\"" |
         \   execute "normal! zz" |
         \ endif

    " PHP Customization
    autocmd FileType php set commentstring=//\ %s
    autocmd FileType php inoremap <Leader>uu <Esc>:call IPhpInsertUse()<Enter>
    autocmd FileType php nnoremap <Leader>uu :call PhpInsertUse()<Enter>
    autocmd FileType php nnoremap <silent> <F1> :call PhpCSFixer()<Enter>

    " Customization
    autocmd FileType sql set commentstring=--\ %s
    autocmd BufRead,BufNewFile *.twig set filetype=html
    autocmd BufRead,BufNewFile *.blade.php set filetype=html
augroup END

augroup ThemeColors
    set background=dark

    " Transparency
    " Use #1D2021 in Terminal
    highlight! Normal guibg=NONE ctermbg=NONE

    " SignColumn with same color of theme
    highlight! link SignColumn LineNr

    " GitGutter with same color of theme
    highlight! GitGutterAdd    guifg=#009900 ctermfg=2
    highlight! GitGutterChange guifg=#bbbb00 ctermfg=3
    highlight! GitGutterDelete guifg=#ff2222 ctermfg=1
    highlight! link GitGutterAddLineNr Underlined
    highlight! link GitGutterChangeLineNr Underlined
    highlight! link GitGutterDeleteLineNr Underlined
    highlight! link GitGutterChangeDeleteLineNr Underlined

    " Syntastic with same color of theme
    highlight! SyntasticErrorSign guifg=#ff2222 ctermfg=1
    highlight! SyntasticWarningSign guifg=#bbbb00 ctermfg=3
augroup END

if filereadable(expand('~/.vimrc.local'))
    source ~/.vimrc.local
endif
